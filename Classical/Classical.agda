{-# OPTIONS --hidden-argument-pun --cubical #-}

module Classical.Classical where

open import Agda.Primitive hiding (Prop) public
open import Cubical.Foundations.Prelude
    renaming (Î£ to Î£' ; I to Interval ; _âˆ¨_ to or ; congL to left
             ; congR to right) public
open import Cubical.HITs.PropositionalTruncation renaming (map to truncMap) public

variable
  l l' al bl cl : Level
  A : Type al
  B : Type bl
  C : Type cl

data âŠ¤ : Type where
 tt : âŠ¤

data âŠ¥ : Type where

Â¬ : Type l â†’ Type l
Â¬ X = X â†’ âŠ¥

Prop : Typeâ‚
Prop = Typeâ‚€

data _ï¼‹_ (A : Type l)(B : Type l') : Type (l âŠ” l' âŠ” (lsuc lzero)) where
 inl : A â†’ A ï¼‹ B
 inr : B â†’ A ï¼‹ B

--------------------------------------------------------
-- Don't use types of Typeâ‚€ that are not propositions --
--------------------------------------------------------
postulate
 lem : (A : Type l) â†’ isProp A â†’ A ï¼‹ (Â¬ A)
 squash : {X : Prop} â†’ isProp X

isPropâŠ¤ : isProp âŠ¤
isPropâŠ¤ tt tt = refl 

isPropâŠ¥ : isProp âŠ¥
isPropâŠ¥ ()

âˆ¥_âˆ¥ : (A : Type l) â†’ Prop
âˆ¥ A âˆ¥ with lem âˆ¥ A âˆ¥â‚ squashâ‚
... | inl x = âŠ¤
... | inr x = âŠ¥

intro : {A : Type l} â†’ A â†’ âˆ¥ A âˆ¥
intro {A} a with lem âˆ¥ A âˆ¥â‚ squashâ‚
... | inl x = tt 
... | inr x = x âˆ£ a âˆ£â‚

_>>_ : {B : Prop} â†’ âˆ¥ A âˆ¥ â†’ (A â†’ B) â†’ B
_>>_ {A} {B} X f with lem âˆ¥ A âˆ¥â‚ squashâ‚
... | inl x = rec squash f x

id : A â†’ A
id x = x

Î£ : {A : Type l} â†’ (P : A â†’ Type l') â†’ Type(l âŠ” l')
Î£ {A} = Î£' A

injective : {A : Type l}{B : Type l'} â†’ (A â†’ B) â†’ Type (l âŠ” l')
injective f = âˆ€ x y â†’ f x â‰¡ f y â†’ x â‰¡ y

surjective : {A : Type l}{B : Type l'} â†’ (A â†’ B) â†’ Type (l âŠ” l')
surjective f = âˆ€ b â†’ Î£ Î» a â†’ f a â‰¡ b

[wts_]_ : (A : Type l) â†’ A â†’ A
[wts _ ] a = a
infixr 0 [wts_]_

_Ã—_ : Type l â†’ Type l' â†’ Type (l âŠ” l')
A Ã— B = Î£ Î»(_ : A) â†’ B
infixr 5 _Ã—_

-- https://en.wikipedia.org/wiki/Fiber_(mathematics)
fiber : {B : Type bl} â†’ (A â†’ B) â†’ B â†’ A â†’ Type bl
fiber f y = Î» x â†’ f x â‰¡ y

embedding : {A : Type al}{B : Type bl} â†’ (A â†’ B) â†’ Type(al âŠ” bl)
embedding f = âˆ€ y â†’ isProp (Î£(fiber f y))

substP : (x : A) â†’ {P Q : A â†’ Type l} â†’ P â‰¡ Q â†’ Q x â†’ P x
substP x Pâ‰¡Q y = transport (Î» i â†’ Pâ‰¡Q (~ i) x) y

-- Modus ponens operator
-- Equivalent to the pipe operator `|>` in F#
_|>_ : A â†’ (A â†’ B) â†’ B
a |> f = f a
infixl 0 _|>_

-- Function application operator (Another modus ponens operator)
-- Equivalent to `$` in Haskell
_$_ : (A â†’ B) â†’ A â†’ B
f $ a = f a
infixr 0 _$_

set : (l : Level) â†’ Type (lsuc(lsuc l))
set l = Type (lsuc l)

_âˆˆ_ : A â†’ (A â†’ Type l) â†’ Type l
_âˆˆ_ = _|>_
infixr 6 _âˆˆ_

_âˆ‰_ :  A â†’ (A â†’ Type l) â†’ Type l
_âˆ‰_ a X = Â¬(a âˆˆ X)
infixr 5 _âˆ‰_

-- Full predicate
ğ“¤ : A â†’ Prop
ğ“¤ = Î» _ â†’ âŠ¤

-- Empty predicate
âˆ… : A â†’ Prop
âˆ… = Î» _ â†’ âŠ¥

-- Propositional Extensionality
propExt' : isProp A â†’ isProp B â†’ (A â†’ B) â†’ (B â†’ A) â†’ A â‰¡ B
propExt' pA pB ab ba = isoToPath (iso ab ba (Î» b â†’ pB (ab (ba b)) b) Î» a â†’ pA (ba (ab a)) a)
  where open import Cubical.Foundations.Isomorphism

LEM : (A : Prop) â†’ A ï¼‹ (Â¬ A)
LEM A = lem A squash

propExt : {A B : Prop} â†’ (A â†’ B) â†’ (B â†’ A) â†’ A â‰¡ B
propExt = propExt' squash squash

âˆƒ : {A : Type l} â†’ (A â†’ Type l') â†’ Prop
âˆƒ P = âˆ¥ Î£ P âˆ¥

â„™ : Type l â†’ Type (l âŠ” (lsuc lzero))
â„™ X = X â†’ Prop

_â‰¢_ : {A : Type l} â†’ A â†’ A â†’ Type l
a â‰¢ b = Â¬(a â‰¡ b)

_âŠ†_ : {A : Type al} â†’ (A â†’ Type l) â†’ (A â†’ Type l') â†’ Type (l âŠ” l' âŠ” al)
A âŠ† B = âˆ€ x â†’ x âˆˆ A â†’ x âˆˆ B

setExt : {X Y : â„™ A} â†’ X âŠ† Y â†’ Y âŠ† X â†’ X â‰¡ Y
setExt XâŠ†Y YâŠ†X = funExt Î» x â†’ propExt (XâŠ†Y x) (YâŠ†X x)

â‹ƒ : â„™(â„™ A) â†’ â„™ A
â‹ƒ P x = âˆƒ Î» Y â†’ x âˆˆ Y Ã— Y âˆˆ P

â‹‚ : â„™(â„™ A) â†’ â„™ A
â‹‚ X = Î» x â†’ âˆ¥ (âˆ€ P â†’ P âˆˆ X â†’ x âˆˆ P) âˆ¥

â‹ƒâˆ…â‰¡âˆ… : â‹ƒ âˆ… â‰¡ âˆ… {A = A}
â‹ƒâˆ…â‰¡âˆ… = funExt Î» x â†’ propExt (_>> Î»(a , xâˆˆa , aâˆˆâˆ…) â†’ aâˆˆâˆ…) Î»()

âˆ…âŠ†X : {X : â„™ A} â†’ âˆ… âŠ† X
âˆ…âŠ†X {X} = Î» x ()

UnionâŠ† : (X : â„™(â„™ A))(Y : â„™ A) â†’ (âˆ€ x â†’ x âˆˆ X â†’ x âŠ† Y) â†’ â‹ƒ X âŠ† Y
UnionâŠ† X Y H a = _>> Î» (Y , aâˆˆY , YâˆˆX) â†’ H Y YâˆˆX a aâˆˆY

_âˆ˜_ : (B â†’ C) â†’ (A â†’ B) â†’ (A â†’ C)
_âˆ˜_ f g x = f (g x) 

âˆ¥map : (A â†’ B) â†’ âˆ¥ A âˆ¥ â†’ âˆ¥ B âˆ¥
âˆ¥map f X = X >> Î» a â†’ intro (f a)

UNREACHABLE : âŠ¥ â†’ {A : Type l} â†’ A
UNREACHABLE ()

mapComp : (f : B â†’ C) (g : A â†’ B) â†’ âˆ¥map (f âˆ˜ g) â‰¡ (âˆ¥map f âˆ˜ âˆ¥map g)
mapComp {B}{C}{A} f g = funExt aux
 where
  aux : (x : âˆ¥ A âˆ¥) â†’ x >> (Î» a â†’ intro (f (g a))) â‰¡ (âˆ¥map f âˆ˜ âˆ¥map g) x
  aux x with lem âˆ¥ A âˆ¥â‚ squashâ‚ | lem âˆ¥ B âˆ¥â‚ squashâ‚ | lem âˆ¥ C âˆ¥â‚ squashâ‚
  ... | inl p | inl q | inl r = isPropâŠ¤ (rec squash (Î» a â†’ tt) p) (rec squash (Î» a â†’ tt) q)
  ... | inl p | inl q | inr r = UNREACHABLE $ r $ truncMap f (truncMap g p)
  ... | inl p | inr q | inl r = UNREACHABLE $ q $ truncMap g p
  ... | inl p | inr q | inr r = UNREACHABLE $ q $ truncMap g p

mapId : âˆ¥map {A = A} id â‰¡ id
mapId {A} = funExt aux
 where
  aux : (x : âˆ¥ A âˆ¥) â†’ âˆ¥map id x â‰¡ x
  aux x with lem âˆ¥ A âˆ¥â‚ squashâ‚
  ... | inl p = isPropâŠ¤ (rec squash (Î» a â†’ tt) p) x

-- Intersection
_âˆ©_ : (A â†’ Type l) â†’ (A â†’ Type l') â†’ A â†’ Type (l âŠ” l')
X âˆ© Y = Î» x â†’ (x âˆˆ X) Ã— (x âˆˆ Y)
infix 7 _âˆ©_

-- Complement
_á¶œ : (A â†’ Type l) â†’ A â†’ Type l
X á¶œ = Î» x â†’ x âˆ‰ X
infix 25 _á¶œ

DNElim : {A : Prop} â†’ Â¬(Â¬ A) â†’ A
DNElim {A} H with LEM A
... | (inl x) = x
... | (inr x) = UNREACHABLE (H x)

DNRule : {A : Prop} â†’ Â¬(Â¬ A) â‰¡ A
DNRule {A} = propExt DNElim Î» z zâ‚ â†’ zâ‚ z

dblCompl : {X : â„™ A} â†’ (X á¶œ)á¶œ â‰¡ X
dblCompl {X} = funExt Î» x â†’ propExt (Î» y â†’ DNElim y) Î» z zâ‚ â†’ zâ‚ z

DeMorgan : {P : A â†’ Type l} â†’ Â¬ (âˆƒ P) â†’ âˆ€ x â†’ Â¬ (P x)
DeMorgan {P} H x G = H (intro(x , G))

DeMorgan2 : {A B : Prop} â†’ Â¬(A Ã— B) â†’ Â¬ A ï¼‹ Â¬ B
DeMorgan2 {A}{B} x with LEM A
... | inl a = inr Î» b â†’ x (a , b)
... | inr Â¬a = inl Î» a â†’ UNREACHABLE $ Â¬a a

DeMorgan3 : {A : Type al} {P : â„™ A} â†’ Â¬(âˆ€ x â†’ P x) â†’ âˆƒ Î» x â†’ Â¬ (P x)
DeMorgan3 H = DNElim Î» X â†’ H Î» x â†’ DNElim (DeMorgan X x)

-- Union
_âˆª_ : (A â†’ Type l) â†’ (A â†’ Type l') â†’ A â†’ Prop
X âˆª Y = Î» x â†’ âˆ¥ (x âˆˆ X) ï¼‹ (x âˆˆ Y) âˆ¥
infix 7 _âˆª_

âˆªComplement : (X : â„™ A) â†’ X âˆª X á¶œ â‰¡ ğ“¤
âˆªComplement X = funExt Î» x â†’ propExt
    (Î» _ â†’ tt) Î» _ â†’ LEM (x âˆˆ X) |> Î»{ (inl p) â†’ intro (inl p)
                                     ; (inr p) â†’ intro (inr p)}
record Associative {A : Type l}(_âˆ™_ : A â†’ A â†’ A) : Type(lsuc l) where
  field
      assoc : (a b c : A) â†’ a âˆ™ (b âˆ™ c) â‰¡ (a âˆ™ b) âˆ™ c
open Associative {{...}} public

-- preimage
_â»Â¹[_] : (f : A â†’ B) â†’ (B â†’ Type l) â†’ (A â†’ Type l)
f â»Â¹[ g ] = g âˆ˜ f

record Commutative {A : Type l}{B : Type l'}(_âˆ™_ : A â†’ A â†’ B) : Type(lsuc (l âŠ” l')) where
  field
    comm : (a b : A) â†’ a âˆ™ b â‰¡ b âˆ™ a
open Commutative {{...}} public

-- Is proposition
record is-prop (A : Type l) : Type l
  where field
   IsProp : isProp A
open is-prop {{...}} public

instance
 -- Intersections are commutative
 âˆ©Comm : Commutative (_âˆ©_ {A = A} {l = lzero})
 âˆ©Comm = record { comm = Î» P Q â†’ funExt (Î» x â†’ propExt (Î»(x , y) â†’ (y , x)) (Î»(x , y) â†’ (y , x))) }

 -- Intersections are associative
 âˆ©assoc : Associative (_âˆ©_ {A = A} {l = lzero})
 âˆ©assoc = record { assoc = Î» a b c â†’ funExt Î» x â†’ propExt (Î» (a , b , c) â†’ ((a , b) , c))
                                                           Î» ((a , b) , c) â†’ (a , b , c) }

 -- Unions are commutative
 âˆªComm : Commutative (_âˆª_ {A = A} {l})
 âˆªComm = record { comm = Î» a b â†’ funExt Î» x â†’ propExt (Î» X â†’ X >> Î»{ (inl p) â†’ intro (inr p)
                                                                    ; (inr p) â†’ intro (inl p)})
                            Î»{p â†’ âˆ¥map (Î»{ (inl x) â†’ inr x ; (inr x) â†’ inl x}) p} }

 -- Unions are associative
 âˆªassoc : Associative (_âˆª_ {A = A})
 âˆªassoc = record { assoc = Î» X Y Z â†’ funExt Î» x â†’
    let H : x âˆˆ X âˆª (Y âˆª Z) â†’ x âˆˆ (X âˆª Y) âˆª Z
        H = Î» p â†’ p >> Î»{ (inl y) â†’ intro (inl (intro (inl y)))
                      ; (inr y) â†’ y >> Î»{ (inl q) â†’ intro (inl (intro (inr q)))
                                                     ; (inr q) â†’ intro (inr q)}}
    in
    let G : x âˆˆ (X âˆª Y) âˆª Z â†’ x âˆˆ X âˆª (Y âˆª Z)
        G = Î» p â†’ p >> Î»{ (inl y) â†’ y >> Î»{ (inl q) â†’ intro $ inl q
                                           ; (inr q) â†’ intro $ inr (intro (inl q))}
                                     ; (inr y) â†’ intro (inr (intro (inr y)))}
    in propExt H G }

-- https://en.wikipedia.org/wiki/Image_(mathematics)
image : (A â†’ B) â†’ B â†’ Prop
image f b = âˆƒ Î» a â†’ f a â‰¡ b

Xâˆ©âˆ…â‰¡âˆ… : {A : Type l} (X : â„™ A) â†’ X âˆ© âˆ… â‰¡ âˆ…
Xâˆ©âˆ…â‰¡âˆ… X = funExt Î» x â†’ propExt (Î»()) Î»()

Pair : A â†’ A â†’ â„™ A
Pair A B X = âˆ¥ (X â‰¡ A) ï¼‹ (X â‰¡ B) âˆ¥

â‹‚lemma : {X : â„™(â„™ A)} â†’ {x : A}
       â†’ x âˆ‰ â‹‚ X â†’ âˆƒ Î» Y â†’ Y âˆˆ X Ã— x âˆ‰ Y
â‹‚lemma {X}{x} xâˆ‰â‹‚X = DNElim Î» p â†’
     let G = DeMorgan p in xâˆ‰â‹‚X (intro Î» P PâˆˆX
   â†’    DeMorgan2 (G P) |> Î»{ (inl Pâˆ‰X) â†’ UNREACHABLE (Pâˆ‰X PâˆˆX)
                            ; (inr Â¬xâˆ‰P) â†’ DNElim Â¬xâˆ‰P})

â‹‚lemma2 : {X : â„™(â„™ A)}
        â†’ (â‹‚ X) á¶œ âˆˆ X â†’ â‹‚ X âŠ† âˆ…
â‹‚lemma2 {X} H = Î» y â†’ _>> Î» (yâˆˆâ‹‚X) â†’
   yâˆˆâ‹‚X ((â‹‚ X) á¶œ) H |> Î»(yâˆ‰â‹‚X) â†’ yâˆ‰â‹‚X (intro yâˆˆâ‹‚X)

â‹‚lemma3 : (â‹‚ ğ“¤) â‰¡ âˆ… {A = A}
â‹‚lemma3 = funExt Î» x â†’ propExt (_>> Î» y â†’ y âˆ… tt) Î»()

â‹‚lemma4 : {A : Type al} â†’ (â‹‚ ğ“¤) á¶œ â‰¡ ğ“¤ {A = A}
â‹‚lemma4 = funExt Î» x â†’ propExt (Î» y â†’ tt) Î» w â†’ _>> Î» y â†’ y âˆ… tt

â‹ƒğ“¤â‰¡ğ“¤ : (â‹ƒ ğ“¤) â‰¡ ğ“¤ {A = A}
â‹ƒğ“¤â‰¡ğ“¤ = funExt Î» x â†’ propExt (Î» y â†’ tt) Î» t â†’ intro (ğ“¤ , t , t)

-- Expressing DeMorgan's Law on arbitrary unions and intersections often results in 
-- an abuse of notation. The following statement is not true when taken literally:
--
--     (â‹‚ X)á¶œ â‰¡ â‹ƒ Xá¶œ
-- 
-- What we really mean is this
--
--     (â‹‚ X)á¶œ â‰¡ â‹ƒ {a | aá¶œ âˆˆ X}
[â‹‚X]á¶œâ‰¡â‹ƒXá¶œ : (X : â„™(â„™ A)) â†’ (â‹‚ X)á¶œ â‰¡ â‹ƒ Î» a â†’ a á¶œ âˆˆ X
[â‹‚X]á¶œâ‰¡â‹ƒXá¶œ X = funExt Î» x â†’ propExt (Î» a â†’
      â‹‚lemma a >> Î»(Y , YâˆˆX , xâˆ‰Y) â†’ intro $ (Y á¶œ) , xâˆ‰Y , ([wts (Y á¶œ)á¶œ âˆˆ X ] subst X (sym dblCompl) YâˆˆX))
      (_>> Î»(Y , xâˆˆY , Yá¶œâˆˆX) â†’ _>> Î» xâˆˆâ‹‚X â†’
      let xâˆˆYá¶œ = xâˆˆâ‹‚X (Y á¶œ) Yá¶œâˆˆX in xâˆˆâ‹‚X (Y á¶œ) Yá¶œâˆˆX xâˆˆY)

cover : {A : Type al} (X : â„™ (â„™ A)) â†’ Type al
cover X = âˆ€ x â†’ x âˆˆ â‹ƒ X

[Xâˆ©Y]á¶œâ‰¡Xá¶œâˆªYá¶œ : (X Y : â„™ A) â†’ (X âˆ© Y)á¶œ â‰¡ X á¶œ âˆª Y á¶œ
[Xâˆ©Y]á¶œâ‰¡Xá¶œâˆªYá¶œ X Y = funExt
 Î» x â†’ propExt (Î» xâˆˆ[Xâˆ©Y]á¶œ â†’ LEM (x âˆˆ Y) |> Î»{ (inl p) â†’ intro (inl (Î» xâˆˆX â†’ xâˆˆ[Xâˆ©Y]á¶œ (xâˆˆX , p)))
                                              ; (inr p) â†’ intro (inr (Î» xâˆˆY â†’ p xâˆˆY)) })
               (_>> Î»{ (inl p) â†’ Î» (xâˆˆX , xâˆˆY) â†’ p xâˆˆX
                     ; (inr p) â†’ Î» (xâˆˆX , xâˆˆY) â†’ p xâˆˆY })

-- https://en.wikipedia.org/wiki/Functor_(functional_programming)
record Functor {Ï : Level â†’ Level}(F : âˆ€{l} â†’ Type l â†’ Type (Ï l)) : TypeÏ‰  where
  field
    map : (A â†’ B) â†’ F A â†’ F B
    compPreserve : (f : B â†’ C) â†’ (g : A â†’ B) â†’ map (f âˆ˜ g) â‰¡ (map f âˆ˜ map g)
    idPreserve : map {A = A} id â‰¡ id
open Functor {{...}} public

-- https://en.wikipedia.org/wiki/Monad_(functional_programming)
record Monad {Ï : Level â†’ Level}(m : âˆ€{l} â†’ Type l â†’ Type (Ï l)) : TypeÏ‰ where
  field
      {{mApp}} : Functor m
      Î¼ : m (m A) â†’ m A -- join
      Î·  : A â†’ m A      -- return
      monadLemma1 : {A : Type al} â†’ Î¼ âˆ˜ Î¼ â‰¡ Î»(a : m(m(m A))) â†’ Î¼ (map Î¼ a)
      monadLemma2 : Î¼ âˆ˜ Î· â‰¡ Î»(a : m A) â†’ a
      monadLemma3 : {A : Type al} â†’ Î¼ âˆ˜ map Î· â‰¡ Î»(a : m A) â†’ a
open Monad {{...}} public

-- bind
_>>=_ : {Ï : Level â†’ Level}{m : âˆ€{l} â†’ Type l â†’ Type (Ï l)} â†’ {{Monad m}}
      â†’ m A â†’ (A â†’ m B) â†’ m B
_>>=_ {m} mA p = Î¼ (map p mA)

-- apply
_<*>_ : {Ï : Level â†’ Level}{m : âˆ€{l} â†’ Type l â†’ Type (Ï l)} â†’ {{Monad m}}
      â†’ m (A â†’ B) â†’ m A â†’ m B
_<*>_ {m} mf mA = mf >>= Î» f â†’ map f mA

instance
 â„™Functor : Functor {Ï = Î» l â†’ l âŠ” lsuc lzero} â„™
 â„™Functor =  record {
    map = Î» f X b â†’ âˆƒ Î» a â†’
      a âˆˆ X Ã— (b â‰¡ f a)
   ; compPreserve = Î» f g â†’ funExt Î» X
                          â†’ funExt Î» y â†’ propExt (_>> Î»(b , H , G)
                          â†’ intro (g b , intro (b , H , refl) , G))
                       (_>> Î»(b , H , G) â†’ H >> Î»(p , pâˆˆX , R) â†’ intro (p , pâˆˆX , (G âˆ™ cong f R)))
   ; idPreserve = funExt Î» X â†’ funExt Î» b â†’ propExt (_>> Î»(x , xâˆˆX , bâ‰¡x) â†’ subst X (sym bâ‰¡x) xâˆˆX)
         Î» bâˆˆX â†’ intro (b , bâˆˆX , refl) }

 â„™Monad : Monad {Ï = Î» l â†’ l âŠ” lsuc lzero} â„™
 â„™Monad = record
           { Î¼ = â‹ƒ 
           ; Î· = Î» a x â†’ âˆ¥ x â‰¡ a âˆ¥
           ; monadLemma1 = funExt Î» X â†’ funExt Î» x â†’ propExt
             (_>> (Î»(P , xâˆˆP , G) â†’
             G >> Î»(G , PâˆˆG , GâˆˆX) â†’
                 intro ( (â‹ƒ G , intro (P , xâˆˆP , PâˆˆG) , intro (G , GâˆˆX , refl)))))
                 ( (_>> Î»(P , xâˆˆP , G) â†’ G >> Î»(G , GâˆˆX , Pâ‰¡âˆªG) â†’
                let H : x âˆˆ â‹ƒ G
                    H = subst (x âˆˆ_) Pâ‰¡âˆªG xâˆˆP in
                  H >> Î»(h , xâˆˆh , hâˆˆG) â†’
                     intro (h , xâˆˆh , intro (G , hâˆˆG , GâˆˆX))))
           ; monadLemma2 =  funExt Î» X â†’ funExt Î» x â†’ propExt
              (_>> Î»(Y , xâˆˆY , Q) â†’ Q >> Î» Yâ‰¡X â†’ substP x (sym Yâ‰¡X) xâˆˆY)
               Î»(xâˆˆX) â†’ intro (X , xâˆˆX , intro refl)
           ; monadLemma3 =  funExt Î» x â†’ funExt Î» y â†’ propExt
            (_>> Î»(Y , yâˆˆY , G) â†’ G >> Î» (h , hâˆˆx , Yâ‰¡[h]) â†’
                let yâˆˆ[h] : y âˆˆ (Î» z â†’ âˆ¥ z â‰¡ h âˆ¥)
                    yâˆˆ[h] = subst (y âˆˆ_) Yâ‰¡[h] yâˆˆY in
               yâˆˆ[h] >> Î» yâ‰¡h â†’ subst x (sym yâ‰¡h) hâˆˆx)
               Î» yâˆˆx â†’ intro ((Î» z â†’ âˆ¥ z â‰¡ y âˆ¥) , intro refl , intro (y , yâˆˆx , refl))
           }

 âˆ¥mapâˆ¥ : Functor âˆ¥_âˆ¥
 âˆ¥mapâˆ¥ = record { map = âˆ¥map
                ; compPreserve = mapComp
                ; idPreserve = mapId 
                }

âˆªpreimage : {A : Type l}{B : Type l'} (X : â„™(â„™ B)) â†’ (f : A â†’ B)
          â†’ f â»Â¹[ â‹ƒ X ] â‰¡ â‹ƒ (map (f â»Â¹[_]) X)
âˆªpreimage X f = funExt Î» z â†’ propExt (_>> Î»(G , (fzâˆˆG) , XâˆˆG)
   â†’ intro ((f â»Â¹[ G ]) , fzâˆˆG , intro (G , XâˆˆG , refl)))
   (_>> Î»(Y , zâˆˆY , Q) â†’ Q >> Î»(h , hâˆˆX , Yâ‰¡fâ»Â¹[h]) â†’ intro (h , ([wts z âˆˆ f â»Â¹[ h ] ]
     substP z (sym Yâ‰¡fâ»Â¹[h]) zâˆˆY) , hâˆˆX))

<*>âˆ…â‰¡âˆ… : {A B : Type (lsuc l)}
        â†’ (P : â„™ (A â†’ B))
        â†’ P <*> âˆ… â‰¡ âˆ…
<*>âˆ…â‰¡âˆ… P = funExt Î» x â†’ propExt (_>> Î»(p , q , r)
                               â†’ r >> Î»(s , t , u)
                               â†’ substP x (sym u) q >> Î»(v , w , x) â†’ w)
                         Î»()

XâŠ†âˆ…â†’Xâ‰¡âˆ… : {X : â„™ A} â†’ X âŠ† âˆ… â†’ X â‰¡ âˆ…
XâŠ†âˆ…â†’Xâ‰¡âˆ… {X} H = funExt Î» x â†’ propExt (Î» xâˆˆX â†’ H x xâˆˆX) Î» ()

âˆ…á¶œâ‰¡ğ“¤ : âˆ… á¶œ â‰¡ ğ“¤ {A = A}
âˆ…á¶œâ‰¡ğ“¤ = funExt Î» x â†’ propExt (Î» z â†’ tt) Î» z â†’ id

record Filter{X : set l}(â„¬ : â„™(â„™ X)) : set l where
 field
  ffull : ğ“¤ âˆˆ â„¬
  fnotâˆ… : âˆ… âˆ‰ â„¬
  finteresect : âˆ€{A B} â†’ A âˆˆ â„¬ â†’ B âˆˆ â„¬ â†’ (A âˆ© B) âˆˆ â„¬
  fax : âˆ€{A B} â†’ A âŠ† B â†’ A âˆˆ â„¬ â†’ B âˆˆ â„¬
open Filter {{...}} public

module _{X : set l}(â„¬ : â„™(â„™ X)){{filter : Filter â„¬}} where
 -- Underlying set for a filter is never empty
 fNonEmpty : âˆ¥ X âˆ¥â‚
 fNonEmpty with lem âˆ¥ X âˆ¥â‚ squashâ‚
 ... | inl p = p
 ... | inr p =
   let H : ğ“¤ â‰¡ âˆ…
       H = funExt Î»(x : X) â†’ UNREACHABLE (p âˆ£ x âˆ£â‚) in
        UNREACHABLE (fnotâˆ… (subst â„¬ H ffull))
 
trivialFilter : {X : set l}
              â†’ âˆ¥ X âˆ¥â‚
              â†’ Filter Î»(Y : â„™ X) â†’ âˆ¥ ğ“¤ âŠ† Y âˆ¥
trivialFilter {X} âˆ¥Xâˆ¥â‚ = record
  { ffull = intro (Î» x z â†’ z)
  ; fnotâˆ… = _>> Î» H â†’ rec squash (Î» z â†’ H z tt) âˆ¥Xâˆ¥â‚
  ; finteresect = Î»{B}{C} â†’ _>> Î» ğ“¤âŠ†B
                          â†’ _>> Î» ğ“¤âŠ†C
                          â†’ intro Î» x z â†’ ğ“¤âŠ†B x z , ğ“¤âŠ†C x z
  ; fax = Î»{B}{C} AâŠ†B â†’ _>> Î» ğ“¤âŠ†B â†’ intro Î» x z â†’ AâŠ†B x (ğ“¤âŠ†B x z)
  }

principalFilter : {X : set l}
                â†’ (A : â„™ X)
                â†’ âˆƒ A
                â†’ Filter Î»(Y : â„™ X) â†’ âˆ¥ A âŠ† Y âˆ¥
principalFilter {X} A âˆƒA = record
  { ffull = intro (Î» x z â†’ tt)
  ; fnotâˆ… = _>> Î» H â†’ âˆƒA >> Î» (x , xâˆˆA) â†’ H x xâˆˆA
  ; finteresect = Î»{B}{C} â†’ _>> Î» AâŠ†B
                â†’ _>> Î» AâŠ†C â†’ intro Î» a aâˆˆA â†’ AâŠ†B a aâˆˆA , AâŠ†C a aâˆˆA
  ; fax = Î»{B}{C} BâŠ†C â†’ _>> Î» AâŠ†B â†’ intro Î» x z â†’ BâŠ†C x (AâŠ†B x z)
  }

record Ideal{X : set l}(â„¬ : â„™(â„™ X)) : set l where
 field
  iempty : âˆ… âˆˆ â„¬
  inotfull : ğ“¤ âˆ‰ â„¬
  iunion : âˆ€{A B} â†’ A âˆˆ â„¬ â†’ B âˆˆ â„¬ â†’ (A âˆª B) âˆˆ â„¬
  iax : âˆ€{A B} â†’ A âŠ† B â†’ B âˆˆ â„¬ â†’ A âˆˆ â„¬
open Ideal {{...}} public

module _{X : set l}(â„¬ : â„™(â„™ X)){{ideal : Ideal â„¬}} where
 -- Underlying set for an ideal is never empty
 iNonEmpty : âˆ¥ X âˆ¥â‚
 iNonEmpty with lem âˆ¥ X âˆ¥â‚ squashâ‚
 ... | inl p = p
 ... | inr p =
   let H : ğ“¤ â‰¡ âˆ…
       H = funExt Î»(x : X) â†’ UNREACHABLE (p âˆ£ x âˆ£â‚) in
        UNREACHABLE (inotfull (subst â„¬ (sym H) iempty))

 Idealá¶œIsFilter : Filter Î» Y â†’ Y á¶œ âˆˆ â„¬
 Idealá¶œIsFilter = record
  { ffull = iax (Î» x z â†’ z tt) iempty
  ; fnotâˆ… = Î» x â†’ inotfull (subst â„¬ âˆ…á¶œâ‰¡ğ“¤ x)
  ; finteresect = Î»{A}{B} Aá¶œâˆˆâ„¬ Bá¶œâˆˆâ„¬ â†’ subst â„¬ (sym ([Xâˆ©Y]á¶œâ‰¡Xá¶œâˆªYá¶œ A B)) (iunion Aá¶œâˆˆâ„¬ Bá¶œâˆˆâ„¬)
  ; fax = Î»{A}{B} AâŠ†B Aá¶œâˆˆâ„¬ â†’ iax (Î» x xâˆˆBá¶œ xâˆˆA â†’ xâˆˆBá¶œ (AâŠ†B x xâˆˆA)) Aá¶œâˆˆâ„¬
  }

principalIdeal : {X : set l}
               â†’ (A : â„™ X)
               â†’ âˆƒ (Î» x â†’ x âˆ‰ A)
               â†’ Ideal Î»(Y : â„™ X) â†’ âˆ¥ Y âŠ† A âˆ¥
principalIdeal {X} A âˆƒÂ¬A = record
 { iempty = intro Î» x â†’ Î» ()
 ; inotfull = _>> Î» ğ“¤âŠ†A â†’ âˆƒÂ¬A >> Î»(x , xâˆ‰A) â†’ xâˆ‰A (ğ“¤âŠ†A x tt)
 ; iunion = Î»{B}{C} â†’ _>> Î» BâŠ†A
                    â†’ _>> Î» CâŠ†A
                    â†’ intro (Î» x â†’ _>> Î»{ (inl xâˆˆB) â†’ BâŠ†A x xâˆˆB
                                        ; (inr xâˆˆC) â†’ CâŠ†A x xâˆˆC}) 
 ; iax = Î»{B}{C} BâŠ†C â†’ _>> Î» CâŠ†A â†’ intro Î» x z â†’ CâŠ†A x (BâŠ†C x z)
 }
